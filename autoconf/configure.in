dnl $Id: configure.in,v 1.22 2002/06/09 02:10:23 wcampbel Exp $
dnl Process this file with autoconf to produce a configure script.
AC_INIT(include/tcm.h)

AC_CONFIG_AUX_DIR(autoconf)
AC_CONFIG_HEADER(include/setup.h)
AC_PREFIX_DEFAULT(`pwd`)

PACKAGE=tcm-hybrid
VERSION=3.1.0-pre
SRCDIR=`pwd`
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(SRCDIR)
AC_SUBST(DPATH)

dnl Checks for programs.
AC_PROG_CC
AC_ISC_POSIX
AC_CANONICAL_SYSTEM
AC_PROG_MAKE_SET
AC_PATH_PROG(RM,rm)
AC_PATH_PROG(CP,cp)
AC_PATH_PROG(SED, sed)
AC_PROG_INSTALL

hold_ldflags=$LDFLAGS     
AC_MSG_CHECKING(for the ld -export-dynamic flag)
LDFLAGS="${LDFLAGS} -Wl,-export-dynamic"
AC_TRY_LINK(, [int i;], found=yes, found=no)
LDFLAGS=$hold_ldflags
AC_MSG_RESULT($found)
if test $found = yes; then
        LDFLAGS="${LDFLAGS} -Wl,-export-dynamic"
fi

dnl Checks for libraries.
dnl Replace `main' with a function in -lnsl:
AC_CHECK_LIB(nsl, gethostbyname)
AC_CHECK_LIB(socket, socket)
AC_CHECK_LIB(resolv, inet_aton)
AC_CHECK_FUNC(crypt,, AC_CHECK_LIB(crypt, crypt,,))

AC_CHECK_FUNC(dlopen,, AC_CHECK_LIB(dl, dlopen))

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/socketvar.h sys/stream.h crypt.h sys/resource.h getopt.h)
AC_CHECK_HEADERS(sys/select.h)

dnl Check for regex.h - if found, assume regexec() and regcomp()
AC_CHECK_HEADER(regex.h, AC_DEFINE(HAVE_REGEX_H), AC_MSG_WARN(regex.h could not be found - regex matching will not be implemented))
dnl Check for stdarg.h - if we can't find it, halt configure
AC_CHECK_HEADER(stdarg.h, , AC_MSG_ERROR(** stdarg.h could not be found - tcm-hybrid will not compile without it **))

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_CHECK_FUNCS(setrlimit strlcat strlcpy)

AC_OUTPUT(Makefile src/Makefile tools/Makefile modules/Makefile)


